plugins {
  id "io.spring.dependency-management" version "1.0.11.RELEASE"
  id "maven-publish"
  id "java-library"
  id "idea"
}

repositories {
  mavenCentral()
}

group = "net.taus.data.marketplace"
version = "git describe --tag --always".execute().text.trim() ?: "0.0.1"

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(14)
  }
  withJavadocJar()
  withSourcesJar()
}

javadoc {
  options.addBooleanOption("html5", true)
}

ext {
  set("springBootVersion", "2.4.4")
  set("springDataVersion", "2020.0.0")
  set("springCloudVersion", "2.3.0")
  set("queryDslVersion", "4.4.0")
}

repositories {
  mavenCentral()
  maven {
    name = "GitHubPackages"
    url = uri("https://maven.pkg.github.com/tausbv/packages")
    credentials(PasswordCredentials)
  }
}

dependencies {
  api "org.springframework.data:spring-data-jpa"
  api "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final"
  api "net.taus.data.language:data-language-client:0.0.10"
  implementation "org.springframework:spring-aspects"
  implementation "org.springframework.boot:spring-boot-starter-cache"
  implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
  implementation "org.springframework.boot:spring-boot-autoconfigure"
  implementation "io.awspring.cloud:spring-cloud-starter-aws-ses"
  implementation "com.sun.mail:javax.mail:1.6.2"
  implementation "org.hibernate:hibernate-hikaricp"
  implementation "org.flywaydb:flyway-core"
  implementation "org.apache.commons:commons-math3:3.6.1"
  implementation "com.fasterxml.jackson.core:jackson-databind"
  implementation "com.google.code.findbugs:jsr305:3.0.2"
  runtimeOnly "mysql:mysql-connector-java"
  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok:1.18.12"
  implementation "com.querydsl:querydsl-jpa"
  implementation "com.querydsl:querydsl-sql-spring:${queryDslVersion}"
  annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}:jpa"
  annotationProcessor "jakarta.persistence:jakarta.persistence-api"
  annotationProcessor "jakarta.annotation:jakarta.annotation-api"
  annotationProcessor "javax.annotation:javax.annotation-api:1.3.2"
  testImplementation "org.springframework.boot:spring-boot-starter-test"
  testImplementation "org.junit.jupiter:junit-jupiter-params"
  testImplementation "org.testcontainers:junit-jupiter"
  testImplementation "org.testcontainers:mysql"
}

test {
  testLogging {
    events "passed", "skipped", "failed"
  }
  useJUnitPlatform()
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    mavenBom "org.springframework.data:spring-data-bom:${springDataVersion}"
    mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:${springCloudVersion}"
    mavenBom "org.testcontainers:testcontainers-bom:1.15.3"
  }
}

publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/tausbv/data-marketplace")
      credentials(PasswordCredentials)
    }
  }
  publications {
    gpr(MavenPublication) {
      from(components.java)
    }
  }
}
